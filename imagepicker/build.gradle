plugins {
    id "com.android.library"
    id "kotlin-android"
    id "kotlin-parcelize"
    id "maven-publish"  // Add this
}

android {
    final sdk = rootProject.ext.sdk

    compileSdk sdk.compileSdk

    defaultConfig {
        minSdk sdk.minSdk
        targetSdk sdk.targetSdk
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    lint {
        abortOnError false
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }

    resourcePrefix "ef_"
    namespace 'com.esafirm.imagepicker'
}

kotlin {
    jvmToolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

// Fixed sourcesJar task
tasks.register("sourcesJar", Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

// Add publishing configuration
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.github.turvoiz'
                artifactId = 'android-image-picker'
                version = 'v1.0.5'

                // Add sources jar
                artifact sourcesJar
            }
        }
    }
}

// Fix task dependencies
tasks.whenTaskAdded { task ->
    if (task.name.contains('generateMetadataFileFor') && task.name.contains('Publication')) {
        task.dependsOn sourcesJar
    }
}

dependencies {
    implementation "com.github.bumptech.glide:glide:4.14.2"
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation "androidx.core:core-ktx:$core_ktx_version"

    testImplementation platform("org.junit:junit-bom:5.9.1")
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation 'com.natpryce:hamkrest:1.8.0.1'
}