plugins {
    id "com.android.library"
    id "kotlin-android"
    id "kotlin-parcelize"
    id "maven-publish"
}

android {
    final sdk = rootProject.ext.sdk

    compileSdk sdk.compileSdk

    defaultConfig {
        minSdk sdk.minSdk
        targetSdk sdk.targetSdk
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    lint {
        abortOnError false
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }

    resourcePrefix "ef_"
    namespace 'com.esafirm.imagepicker'
}

kotlin {
    jvmToolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

// Create sourcesJar task
tasks.register("sourcesJar", Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

// Publishing configuration
afterEvaluate {
    publishing {
        publications {
            imagepicker(MavenPublication) {
                groupId = 'com.github.turvoiz'
                artifactId = 'android-image-picker'
                version = 'v1.0.8'

                // Add artifacts manually instead of using components.release
                artifact bundleReleaseAar
                artifact sourcesJar

                // Add POM configuration
                pom {
                    packaging = 'aar'
                    name = 'Android Image Picker'
                    description = 'A simple library to select images from gallery and camera'
                }
            }
        }
    }
}

dependencies {
    implementation "com.github.bumptech.glide:glide:4.14.2"
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation "androidx.core:core-ktx:$core_ktx_version"

    testImplementation platform("org.junit:junit-bom:5.9.1")
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation 'com.natpryce:hamkrest:1.8.0.1'
}